{
 "cells": [
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Jutul\n",
    "using JutulDarcy\n",
    "using LinearAlgebra\n",
    "using GLMakie"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "# Example demonstrating optimzation of parameters against observations\n",
    "We create a simple test problem: A 1D nonlinear displacement. The observations\n",
    "are generated by solving the same problem with the true parameters. We then\n",
    "match the parameters against the observations using a different starting guess\n",
    "for the parameters, but otherwise the same physical description of the system."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function setup_bl(;nc = 100, time = 1.0, nstep = 100, poro = 0.1, perm = 9.8692e-14)\n",
    "    T = time\n",
    "    tstep = repeat([T/nstep], nstep)\n",
    "    G = get_1d_reservoir(nc, poro = poro, perm = perm)\n",
    "    nc = number_of_cells(G)\n",
    "\n",
    "    bar = 1e5\n",
    "    p0 = 1000*bar\n",
    "    sys = ImmiscibleSystem((LiquidPhase(), VaporPhase()))\n",
    "    model = SimulationModel(G, sys)\n",
    "    model.primary_variables[:Pressure] = Pressure(minimum = -Inf, max_rel = nothing)\n",
    "    kr = BrooksCoreyRelativePermeabilities(sys, [2.0, 2.0])\n",
    "    replace_variables!(model, RelativePermeabilities = kr)\n",
    "    tot_time = sum(tstep)\n",
    "\n",
    "    parameters = setup_parameters(model, PhaseViscosities = [1e-3, 5e-3]) # 1 and 5 cP\n",
    "    state0 = setup_state(model, Pressure = p0, Saturations = [0.0, 1.0])\n",
    "\n",
    "    irate = 100*sum(parameters[:FluidVolume])/tot_time\n",
    "    src  = [SourceTerm(1, irate, fractional_flow = [1.0-1e-3, 1e-3]),\n",
    "            SourceTerm(nc, -irate, fractional_flow = [1.0, 0.0])]\n",
    "    forces = setup_forces(model, sources = src)\n",
    "\n",
    "    return (model, state0, parameters, forces, tstep)\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Number of cells and time-steps"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "N = 100\n",
    "Nt = 100\n",
    "poro_ref = 0.1\n",
    "perm_ref = 9.8692e-14"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Set up and simulate reference"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "model_ref, state0_ref, parameters_ref, forces, tstep = setup_bl(nc = N, nstep = Nt, poro = poro_ref, perm = perm_ref)\n",
    "states_ref, = simulate(state0_ref, model_ref, tstep, parameters = parameters_ref, forces = forces, info_level = -1)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Set up another case where the porosity is different"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "model, state0, parameters, = setup_bl(nc = N, nstep = Nt, poro = 2*poro_ref, perm = 1.0*perm_ref)\n",
    "states, rep = simulate(state0, model, tstep, parameters = parameters, forces = forces, info_level = -1)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Plot the results"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "fig = Figure()\n",
    "ax = Axis(fig[1, 1], title = \"Saturation\")\n",
    "lines!(ax, states_ref[end][:Saturations][1, :], label = \"Reference\")\n",
    "lines!(ax, states[end][:Saturations][1, :], label = \"Initial guess\")\n",
    "axislegend(ax)\n",
    "ax = Axis(fig[1, 2], title = \"Pressure\")\n",
    "lines!(ax, states_ref[end][:Pressure], label = \"Reference\")\n",
    "lines!(ax, states[end][:Pressure], label = \"Initial guess\")\n",
    "axislegend(ax)\n",
    "fig"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Define objective function\n",
    "Define objective as mismatch between water saturation in current state and\n",
    "reference state. The objective function is currently a sum over all time\n",
    "steps. We implement a function for one term of this sum."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function mass_mismatch(m, state, dt, step_no, forces)\n",
    "    state_ref = states_ref[step_no]\n",
    "    fld = :Saturations\n",
    "    val = state[fld]\n",
    "    ref = state_ref[fld]\n",
    "    err = 0\n",
    "    for i in axes(val, 2)\n",
    "        err += (val[1, i] - ref[1, i])^2\n",
    "    end\n",
    "    return dt*err\n",
    "end\n",
    "@assert Jutul.evaluate_objective(mass_mismatch, model, states_ref, tstep, forces) == 0.0\n",
    "@assert Jutul.evaluate_objective(mass_mismatch, model, states, tstep, forces) > 0.0"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Set up a configuration for the optimization\n",
    "\n",
    "The optimization code enables all parameters for optimization by default, with\n",
    "relative box limits 0.1 and 10 specified here. If use_scaling is enabled the\n",
    "variables in the optimization are scaled so that their actual limits are\n",
    "approximately box limits.\n",
    "\n",
    "We are not interested in matching gravity effects or viscosity here.\n",
    "Transmissibilities are derived from permeability and varies significantly. We\n",
    "can set log scaling to get a better conditioned optimization system, without\n",
    "changing the limits or the result."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "cfg = optimization_config(model, parameters, use_scaling = true, rel_min = 0.1, rel_max = 10)\n",
    "for (ki, vi) in cfg\n",
    "    if ki in [:TwoPointGravityDifference, :PhaseViscosities]\n",
    "        vi[:active] = false\n",
    "    end\n",
    "    if ki == :Transmissibilities\n",
    "        vi[:scaler] = :log\n",
    "    end\n",
    "end\n",
    "print_obj = 100"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Set up parameter optimization\n",
    "\n",
    "This gives us a set of function handles together with initial guess and limits.\n",
    "Generally calling either of the functions will mutate the data Dict. The options are:\n",
    "F_o(x) -> evaluate objective\n",
    "dF_o(dFdx, x) -> evaluate gradient of objective, mutating dFdx (may trigger evaluation of F_o)\n",
    "F_and_dF(F, dFdx, x) -> evaluate F and/or dF. Value of nothing will mean that the corresponding entry is skipped."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "F_o, dF_o, F_and_dF, x0, lims, data = setup_parameter_optimization(model, state0, parameters, tstep, forces, mass_mismatch, cfg, print = print_obj, param_obj = true);\n",
    "F_initial = F_o(x0)\n",
    "dF_initial = dF_o(similar(x0), x0)\n",
    "@info \"Initial objective: $F_initial, gradient norm $(norm(dF_initial))\""
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Link to an optimizer package\n",
    "We use Optim.jl but the interface is general enough that e.g. LBFGSB.jl can\n",
    "easily be swapped in.\n",
    "\n",
    "LBFGS is a good choice for this problem, as Jutul provides sensitivities via\n",
    "adjoints that are inexpensive to compute."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Optim\n",
    "lower, upper = lims\n",
    "inner_optimizer = LBFGS()\n",
    "opts = Optim.Options(store_trace = true, show_trace = true, time_limit = 30)\n",
    "results = optimize(Optim.only_fg!(F_and_dF), lower, upper, x0, Fminbox(inner_optimizer), opts)\n",
    "x = results.minimizer\n",
    "display(results)\n",
    "F_final = F_o(x)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Compute the solution using the tuned parameters found in x."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "parameters_t = deepcopy(parameters)\n",
    "devectorize_variables!(parameters_t, model, x, data[:mapper], config = data[:config])\n",
    "x_truth = vectorize_variables(model_ref, parameters_ref, data[:mapper], config = data[:config])\n",
    "\n",
    "states_tuned, = simulate(state0, model, tstep, parameters = parameters_t, forces = forces, info_level = -1);\n",
    "nothing"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Plot final parameter spread"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "@info \"Final residual $F_final (down from $F_initial)\"\n",
    "fig = Figure()\n",
    "ax1 = Axis(fig[1, 1], title = \"Scaled parameters\", ylabel = \"Value\")\n",
    "scatter!(ax1, x, label = \"Final X\")\n",
    "scatter!(ax1, x0, label = \"Initial X\")\n",
    "lines!(ax1, lower, label = \"Lower bound\")\n",
    "lines!(ax1, upper, label = \"Upper bound\")\n",
    "axislegend()\n",
    "fig"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Plot the final solutions.\n",
    "Note that we only match saturations - so any match in pressure\n",
    "is not guaranteed."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "fig = Figure()\n",
    "ax = Axis(fig[1, 1], title = \"Saturation\")\n",
    "lines!(ax, states_ref[end][:Saturations][1, :], label = \"Reference\")\n",
    "lines!(ax, states[end][:Saturations][1, :], label = \"Initial guess\")\n",
    "lines!(ax, states_tuned[end][:Saturations][1, :], label = \"Tuned\")\n",
    "\n",
    "axislegend(ax)\n",
    "ax = Axis(fig[1, 2], title = \"Pressure\")\n",
    "lines!(ax, states_ref[end][:Pressure], label = \"Reference\")\n",
    "lines!(ax, states[end][:Pressure], label = \"Initial guess\")\n",
    "lines!(ax, states_tuned[end][:Pressure], label = \"Tuned\")\n",
    "axislegend(ax)\n",
    "fig"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Plot the objective history and function evaluations"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "fig = Figure()\n",
    "ax1 = Axis(fig[1, 1], yscale = log10, title = \"Objective evaluations\", xlabel = \"Iterations\", ylabel = \"Objective\")\n",
    "plot!(ax1, data[:obj_hist][2:end])\n",
    "ax2 = Axis(fig[1, 2], yscale = log10, title = \"Outer optimizer\", xlabel = \"Iterations\", ylabel = \"Objective\")\n",
    "t = map(x -> x.value, Optim.trace(results))\n",
    "plot!(ax2, t)\n",
    "fig"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.0"
  },
  "kernelspec": {
   "name": "julia-1.11",
   "display_name": "Julia 1.11.0",
   "language": "julia"
  }
 },
 "nbformat": 4
}
